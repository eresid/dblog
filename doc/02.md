### Обробка помилок

У цьому уроці ми будемо коректно обробляти помилки. Для цього потрібно розділити їх на помилки REST API та Web.
У першому випадку потрібно показувати JSON певного формату, у другому html сторінку.

#### Код

Спочатку створимо об'єкт, з якого будемо будувати наш JSON для помилок на стороні REST API:
```D
module api.ErrorObject;

struct ErrorObject {
	int status;
	string message; 
}
```

А також наш [Diet](http://vibed.org/templates/diet) файл, який буде описувати html сторінку з помилкою:

```
doctype html
html
	head
		- auto title = "HTTP error " ~ to!string(error.code);
		title #{title}
	body
		p An error has occured. We are really sorry about that!
		p Error description: #{error.message}
```

Для автоматичного перетворення ваших html файлів у формат Diet можна використовувати сайт [html2jade.org](http://html2jade.org/).

Потім створимо основний файл:

```D
import std.algorithm : startsWith;
import std.conv;
import std.process;

import diet.html;
import vibe.stream.wrapper;
import vibe.d;

import api.Api;
import api.ErrorObject;
import api.ServerApi;

shared static this()
{
    auto router = new URLRouter;
    router.registerRestInterface(new ServerApi);
    router.get("*", serveStaticFiles("public/"));
    router.get("/", &index);

    auto settings = new HTTPServerSettings;
    settings.port = 8080;
    settings.bindAddresses = ["::1", "127.0.0.1"];
    settings.errorPageHandler = toDelegate(&errorPage);
    listenHTTP(settings, router);

    logInfo("Please open http://127.0.0.1:8080/ in your browser.");
}

void index(HTTPServerRequest req, HTTPServerResponse res)
{
    auto dst = StreamOutputRange(res.bodyWriter);
    int iterations = 10;
    dst.compileHTMLDietFile!("index.dt", iterations);
}

void errorPage(HTTPServerRequest req, HTTPServerResponse res, HTTPServerErrorInfo error)
{
    ErrorObject errorObject = ErrorObject(error.code, error.message);

    // У даному прикладі REST API мають префікс "/api/v1/", тому
    // за допомогою наступного рядка розділяємо API запити від
    // звичайних і показувати або потрібний нам JSON або html сторінку.
    if (req.path.startsWith("/api/v"))
    {
        res.writeBody(errorObject.serializeToJsonString(), "application/json");
    }
    else
    {
        res.render!("error.dt", req, error);
    }
}

```
